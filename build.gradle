plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'org.cadixdev.licenser' version '0.6.1'

    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

license {
    header(project.file('NOTICE'))

    ext['year'] = Calendar.getInstance().get(Calendar.YEAR)
    ext['name'] = 'TheSilkMiner'
    ext['app'] = 'Rolls o\' Drops'
}

group = 'net.thesilkminer.mc.austin'
version = '1.0-SNAPSHOT'

println "Setting up ${project.name} as mod '${modId}' for Minecraft ${mcVersion} with forge version '${forgeVersion}'"

java {
    archivesBaseName = modId
    toolchain.languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_17.majorVersion)
}

minecraft {
    mappings channel: 'parchment', version: "${parchmentReleaseDate}-${mcVersion}"

    runs {
        all {
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            lazyToken('minecraft_classpath') {
                configurations.runtimeClasspath.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
            }

            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }

        client {
            workingDirectory project.file('runClient')
        }

        server {
            workingDirectory project.file('runServer')
        }

        data {
            workingDirectory project.file('runData')

            args '--mod', modId, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'CurseMaven'
        url 'https://cursemaven.com'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mcVersion}-${forgeVersion}"

    implementation fg.deobf('curse.maven:aplp-584751:3666227')

    runtimeOnly fg.deobf('curse.maven:jei-238222:3672780')
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : modId,
                "Specification-Vendor"    : "TheSilkMiner, N1Kx",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "TheSilkMiner, N1Kx",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Automatic-Module-Name"   : "net.thesilkminer.mc.rollsodrops"
        ])
    }
    finalizedBy('reobfJar')
}
